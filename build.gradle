buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath group: 'pl.allegro.tech.build',   name: 'axion-release-plugin', version: '1.10.2'
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.12.0'
        classpath group: 'com.bmuschko', name: 'gradle-nexus-plugin', version: '2.3.1'
    }
}

plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.10.2'
    // TODO(https://github.com/allegro/envoy-control/issues/102):
    //   upgrade to latest 1.3.x for bugfixes and performance improvements
    id 'org.jetbrains.kotlin.jvm' version '1.3.0'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.3.0'
    id 'org.jetbrains.kotlin.plugin.allopen' version '1.3.0'
    id "org.jlleitschuh.gradle.ktlint" version "6.3.1"
    id "org.jlleitschuh.gradle.ktlint-idea" version "6.3.1"
    id "io.gitlab.arturbosch.detekt" version "1.0.0-RC11"
}

apply plugin: 'io.codearte.nexus-staging'

scmVersion {
    tag {
        prefix = project.rootProject.name
    }
    versionCreator 'versionWithBranch'
}

allprojects {

    project.group = 'pl.allegro.tech.servicemesh'
    project.version = scmVersion.version

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
            mavenContent { snapshotsOnly() }
        }
    }

    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'

    project.ext.versions = [
            // TODO(https://github.com/allegro/envoy-control/issues/102):
            //   upgrade to latest 1.3.x for bugfixes and performance improvements
            kotlin            : '1.3.0',
            java_controlplane : '0.1.24-SNAPSHOT',
            spring_boot       : '2.1.5.RELEASE',
            grpc              : '1.21.0',
            jaxb              : '2.3.1',
            javaxactivation   : '1.1.1',
            micrometer        : '1.1.2',
            dropwizard        : '4.0.6',
            ecwid_consul      : '1.4.1',
            awaitility        : '3.1.3',
            embedded_consul   : '2.0.0',
            junit             : '5.5.2',
            assertj           : '3.11.1',
            jackson           : '2.9.10',
            toxiproxy         : '2.1.3',
            testcontainers    : '1.12.5',
            reactor           : '3.2.5.RELEASE',
            consul_recipes    : '0.8.3',
            mockito           : '2.23.0',
            cglib             : '3.2.9',
            logback           : '1.2.3',
            slf4j             : '1.7.25',
            re2j              : '1.3',
            xxhash            : '0.10.1'
    ]
}

subprojects {

    apply plugin: 'maven-publish'
    apply plugin: 'org.jlleitschuh.gradle.ktlint'
    apply plugin: 'io.gitlab.arturbosch.detekt'

    sourceCompatibility = 1.8
    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat = 'full'
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from project.components.java
            }
        }
    }

    configurations {
        compile.exclude group: 'commons-logging', module: 'commons-logging'
        compile.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        compile.exclude group: 'org.slf4j', module: 'slf4j-jcl'
        compile.exclude group: 'log4j', module: 'log4j'
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = '1.8'
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = '1.8'
        }
    }

    dependencies {
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
        testCompile group: 'org.assertj', name: 'assertj-core', version: versions.assertj
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
    }

    detekt {
        toolVersion = "1.0.0-RC11"
        input = files("src/main/kotlin", "src/test/kotlin")
        filters = ".*/resources/.*,.*/build/.*"
        config = files("$rootDir/config/detekt/default-detekt-config.yml", "$rootDir/config/detekt/detekt-config.yml")
    }
}

wrapper {
    gradleVersion = '5.6.2'
}

subprojects {
    apply plugin: 'com.bmuschko.nexus'

    modifyPom {
        project {
            name project.name
            description 'Production-ready Control Plane for Service Mesh based on Envoy Proxy.'
            url 'https://github.com/allegro/envoy-control'
            inceptionYear '2019'

            scm {
                url 'https://github.com/allegro/envoy-control'
                connection 'scm:git@github.com:allegro/envoy-control.git'
                developerConnection 'scm:git@github.com:allegro/envoy-control.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }

            developers {
                developer {
                    id 'skylab'
                    name 'Team Skylab-Helix'
                }
            }
        }
    }
}

nexusStaging {
    packageGroup = "pl.allegro"

    numberOfRetries = 50
    delayBetweenRetriesInMillis = 5000
}
