
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'pl.allegro.tech.build',   name: 'axion-release-plugin', version: '1.13.3'
    }
}

plugins {
    id 'signing'

    id 'pl.allegro.tech.build.axion-release' version '1.13.3'
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.6.10'
    id 'org.jetbrains.kotlin.plugin.allopen' version '1.6.10'
    id "org.jlleitschuh.gradle.ktlint" version "10.2.1"
    id "org.jlleitschuh.gradle.ktlint-idea" version "10.2.0"
    id "io.gitlab.arturbosch.detekt" version "1.18.0"
    id 'io.github.gradle-nexus.publish-plugin' version '1.0.0'

}

scmVersion {
    tag {
        prefix = "envoy-control-"
    }
    versionCreator 'versionWithBranch'
}

allprojects {

    project.group = 'pl.allegro.tech.servicemesh'
    project.version = scmVersion.version

    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
            mavenContent { snapshotsOnly() }
        }
    }

    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'

    project.ext.versions = [
            kotlin            : '1.6.10',
            java_controlplane : '1.0.37',
            spring_boot       : '2.3.4.RELEASE',
            grpc              : '1.48.1',
            jaxb              : '2.3.1',
            javaxactivation   : '1.2.0',
            micrometer        : '1.5.5',
            dropwizard        : '4.1.12.1',
            ecwid_consul      : '1.4.1',
            awaitility        : '4.0.3',
            embedded_consul   : '2.0.0',
            junit             : '5.6.2',
            assertj           : '3.16.1',
            jackson           : '2.11.2',
            toxiproxy         : '2.1.3',
            testcontainers    : '1.16.0',
            reactor           : '3.3.10.RELEASE',
            consul_recipes    : '0.9.1',
            mockito           : '3.3.3',
            cglib             : '3.2.9',
            logback           : '1.2.3',
            slf4j             : '1.7.30',
            re2j              : '1.3',
            xxhash            : '0.10.1',
            okhttp            : '4.9.0'
    ]
}


nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv("SONATYPE_USERNAME")
            password = System.getenv("SONATYPE_PASSWORD")
        }
    }
}

subprojects {

    apply plugin: 'maven-publish'
    apply plugin: 'org.jlleitschuh.gradle.ktlint'
    apply plugin: 'io.gitlab.arturbosch.detekt'
    apply plugin: 'signing'

    sourceCompatibility = JavaVersion.VERSION_11
    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    ktlint {
        enableExperimentalRules = false
        verbose = true
        disabledRules = ["parameter-list-wrapping", "indent", "colon-spacing", "import-ordering"]
    }

    test {
        jvmArgs('-Djunit.jupiter.extensions.autodetection.enabled=true')
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat = 'full'
        }
    }

    if (System.getenv("GPG_KEY_ID")) {
        signing {
            useInMemoryPgpKeys(
                System.getenv("GPG_KEY_ID"),
                System.getenv("GPG_PRIVATE_KEY"),
                System.getenv("GPG_PRIVATE_KEY_PASSWORD")
            )
            sign publishing.publications
        }
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    publishing {
        publications {
            sonatype(MavenPublication) { publication ->
                from project.components.java
                pom {
                    name = project.name
                    description = 'Production-ready Control Plane for Service Mesh based on Envoy Proxy.'
                    url ='https://github.com/allegro/envoy-control'
                    inceptionYear = '2019'

                    scm {
                        url = 'https://github.com/allegro/envoy-control'
                        connection = 'scm:git@github.com:allegro/envoy-control.git'
                        developerConnection = 'scm:git@github.com:allegro/envoy-control.git'
                    }

                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'skylab'
                            name = 'Team Skylab-Helix'
                        }
                    }
                }
            }
        }

    }

    configurations {
        compile.exclude group: 'commons-logging', module: 'commons-logging'
        compile.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        compile.exclude group: 'org.slf4j', module: 'slf4j-jcl'
        compile.exclude group: 'log4j', module: 'log4j'
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = '11'
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = '11'
        }
    }

    dependencies {
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
        testImplementation group: 'org.assertj', name: 'assertj-core', version: versions.assertj
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
    }

    detekt {
        toolVersion = "1.18.0"
        input = files("src/main/kotlin", "src/test/kotlin")
        config = files("$rootDir/config/detekt/detekt.yml")
        buildUponDefaultConfig = true
    }
}

wrapper {
    gradleVersion = '7.1.1'
}
