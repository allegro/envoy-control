buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'pl.allegro.tech.build',   name: 'axion-release-plugin', version: '1.10.2'
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.12.0'
        classpath group: 'com.bmuschko', name: 'gradle-nexus-plugin', version: '2.3.1'
    }
}

plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.10.2'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.3.72'
    id 'org.jetbrains.kotlin.plugin.allopen' version '1.3.72'
    id "org.jlleitschuh.gradle.ktlint" version "9.4.0"
    id "org.jlleitschuh.gradle.ktlint-idea" version "9.4.0"
    id "io.gitlab.arturbosch.detekt" version "1.13.1"
}

apply plugin: 'io.codearte.nexus-staging'

scmVersion {
    tag {
        prefix = project.rootProject.name
    }
    versionCreator 'versionWithBranch'
}

allprojects {

    project.group = 'pl.allegro.tech.servicemesh'
    project.version = scmVersion.version

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
            mavenContent { snapshotsOnly() }
        }
    }

    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'

    project.ext.versions = [
            kotlin            : '1.3.72',
            java_controlplane : '0.1.24-optimization-SNAPSHOT',
            spring_boot       : '2.3.4.RELEASE',
            grpc              : '1.21.0',
            ecwid_consul      : '1.4.1',
            embedded_consul   : '2.0.0',
            junit             : '5.5.2',
            toxiproxy         : '2.1.3',
            testcontainers    : '1.12.5',
            consul_recipes    : '0.8.3',
            mockito           : '2.23.0',
            cglib             : '3.2.9',
            re2j              : '1.3',
            xxhash            : '0.10.1'
    ]
}

subprojects {

    apply plugin: 'maven-publish'
    apply plugin: 'org.jlleitschuh.gradle.ktlint'
    apply plugin: 'io.gitlab.arturbosch.detekt'

    sourceCompatibility = 1.8
    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    ktlint {
        enableExperimentalRules = false
        verbose = true
        disabledRules = ["parameter-list-wrapping", "indent", "colon-spacing", "import-ordering"]
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat = 'full'
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from project.components.java
            }
        }
    }

    configurations {
        compile.exclude group: 'commons-logging', module: 'commons-logging'
        compile.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        compile.exclude group: 'org.slf4j', module: 'slf4j-jcl'
        compile.exclude group: 'log4j', module: 'log4j'
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = '1.8'
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = '1.8'
        }
    }

    dependencies {
        implementation platform(group: 'org.springframework.boot', name: 'spring-boot-dependencies', version: versions.spring_boot)

        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
        testCompile group: 'org.assertj', name: 'assertj-core'
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
    }

    detekt {
        toolVersion = "1.13.1"
        input = files("src/main/kotlin", "src/test/kotlin")
        config = files("$rootDir/config/detekt/detekt.yml")
        buildUponDefaultConfig = true
    }
}

wrapper {
    gradleVersion = '6.6.1'
}

subprojects {
    apply plugin: 'com.bmuschko.nexus'

    modifyPom {
        project {
            name project.name
            description 'Production-ready Control Plane for Service Mesh based on Envoy Proxy.'
            url 'https://github.com/allegro/envoy-control'
            inceptionYear '2019'

            scm {
                url 'https://github.com/allegro/envoy-control'
                connection 'scm:git@github.com:allegro/envoy-control.git'
                developerConnection 'scm:git@github.com:allegro/envoy-control.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }

            developers {
                developer {
                    id 'skylab'
                    name 'Team Skylab-Helix'
                }
            }
        }
    }
}

nexusStaging {
    packageGroup = "pl.allegro"

    numberOfRetries = 50
    delayBetweenRetriesInMillis = 5000
}
