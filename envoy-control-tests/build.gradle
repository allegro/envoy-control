dependencies {
    implementation project(':envoy-control-runner')

    implementation group: 'org.assertj', name: 'assertj-core', version: versions.assertj
    implementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
    implementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
    implementation group: 'org.awaitility', name: 'awaitility', version: versions.awaitility
    implementation(group: 'com.pszymczyk.consul', name: 'embedded-consul', version: versions.embedded_consul) {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: versions.okhttp

    implementation "org.apache.httpcomponents:httpcore:4.4.15"
    implementation "org.apache.httpcomponents:httpclient:4.5.14"

    implementation group: 'eu.rekawek.toxiproxy', name: 'toxiproxy-java', version: versions.toxiproxy
    runtimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
    implementation group: 'org.testcontainers', name: 'junit-jupiter', version: versions.testcontainers
    implementation group: 'org.testcontainers', name: 'testcontainers', version: versions.testcontainers
}

test {
    useJUnitPlatform {
        excludeTags 'reliability'
    }
    maxParallelForks = 1
    testClassesDirs = project.sourceSets.main.output.classesDirs
}

task reliabilityTest(type: Test) {
    systemProperty 'RELIABILITY_FAILURE_DURATION_SECONDS', System.getProperty('RELIABILITY_FAILURE_DURATION_SECONDS', '300')
    useJUnitPlatform {
        includeTags 'reliability'
    }

    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = 'full'
    }
    testClassesDirs = project.sourceSets.main.output.classesDirs
}
