plugins {
    id 'org.jetbrains.kotlin.jvm'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':envoy-control-runner')

    implementation group: 'org.assertj', name: 'assertj-core'
    implementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
    implementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
    implementation group: 'org.awaitility', name: 'awaitility'
    implementation(group: 'com.pszymczyk.consul', name: 'embedded-consul', version: versions.embedded_consul) {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }

    implementation "org.apache.httpcomponents:httpcore:4.4.9"
    implementation "org.apache.httpcomponents:httpclient:4.5.5"

    implementation group: 'eu.rekawek.toxiproxy', name: 'toxiproxy-java', version: versions.toxiproxy
    runtimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
    implementation group: 'org.testcontainers', name: 'junit-jupiter', version: versions.testcontainers
    implementation group: 'org.testcontainers', name: 'testcontainers', version: versions.testcontainers
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

test {
    useJUnitPlatform {
        excludeTags 'reliability'
    }
    maxParallelForks = 1
    testClassesDirs = project.sourceSets.main.output.classesDirs
}

task reliabilityTest(type: Test) {
    systemProperty 'RELIABILITY_FAILURE_DURATION_SECONDS', System.getProperty('RELIABILITY_FAILURE_DURATION_SECONDS', '300')
    useJUnitPlatform {
        includeTags 'reliability'
    }

    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = 'full'
    }
    testClassesDirs = project.sourceSets.main.output.classesDirs
}
